apply plugin: 'com.android.application'
apply plugin: 'maven-publish'

version '0.9.0-SNAPSHOT'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.rex.qly"

        minSdkVersion 21
        targetSdkVersion 29

        versionName project.version
        versionCode Integer.parseInt(([ 'git','rev-list','--count','HEAD'].execute().text).trim())

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    //implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.navigation:navigation-fragment:2.2.2'
    implementation 'androidx.navigation:navigation-ui:2.2.2'

    implementation 'com.google.android.material:material:1.1.0'

    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.github.tony19:logback-android:2.0.0'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

afterEvaluate {
    android.applicationVariants.all { variant ->
        //println(“variant.name:" + variant.name)
        //println(“variant.baseName:" + variant.baseName)
        //println("variant.flavorName:" + variant.flavorName)

        if (!variant.buildType.isDebuggable()) {
            publishing.publications.create(variant.name, MavenPublication) {
                groupId "com.rex"
                artifactId "qly"

                // Since Android Gradle 3.6.0
                // Ref: https://developer.android.google.cn/studio/build/maven-publish-plugin?hl=zh-cn
                // Will generate ZIP contains APK and mapping TXT
                //from components.getByName("${variant.name}_apk")

                // Publish default APK package, for ApplicationVariant ApkVariant
                artifact (new File(variant.packageApplicationProvider.get().getOutputDirectory().getAsFile().get(), variant.outputs[0].outputFileName)) {
                    builtBy variant.assembleProvider.get()
                }

                // Publish mapping TXT
                if (!variant.getMappingFileProvider().get().isEmpty()) {
                    artifact project.tasks.create("generateMappingZipFor${name.capitalize()}Publication", Zip) {
                        classifier "mapping"
                        group "publishing"
                        baseName variant.baseName // Default ${project.name}-mapping.zip may conflict with other flavors
                        from variant.getMappingFileProvider().get()
                    }
                }

                // Publish native stack ZIP
                if (! variant.externalNativeBuildProviders.isEmpty()) {
                    artifact project.tasks.create("generateStackZipFor${name.capitalize()}Publication", Zip) {
                        classifier "stack"
                        group "publishing"
                        baseName variant.baseName // Default ${project.name}-stack.zip may conflict with other flavors
                        variant.externalNativeBuildProviders.each { provider ->
                            from provider.get().objFolder
                        }
                    }
                }
            }
        }
    }
}
